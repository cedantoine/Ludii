(game "Margo" 
    (players 2) 
    (equipment { 
        (board (square 7 pyramidal:True) use:Vertex) 
        (piece "Ball" Each) 
    }) 
    (rules
    	(meta (swap))
        (play
    		(move Add 
        		(to (sites Empty) 
        			if:(and 
        					(is Flat)
        					(or {
        						(is Freedom (sites Group from:(intersection {(sites Around (to)) (sites Occupied by:Mover)}) isVisible:True) (to))
        						(> (count Sites in:(intersection {(sites Empty) (sites Around (to)) (sites Layer (layer of:0))})) 0)
        						(and {
        							(not (is Freedom (sites Group from:(intersection {(sites Around (to)) (sites Occupied by:Next)}) isVisible:True) (to))) 
        							(> (count Sites in:(sites Group from:(intersection {(sites Around (to)) (sites Occupied by:Next)}) isVisible:True)) 0)
        							(< 
        								(count Sites in:(intersection {(sites Around (to)) (sites Occupied by:Next) (sites Support (id Mover))}))
        								(count Sites in:(intersection {(sites Around (to)) (sites Occupied by:Next)}))
        							)
        						})
        					})
        			   )
                )
        		(then
        			(forEach Site (sites Occupied by:Next) 
        				(if
    						(and (not (is Freedom (sites Group at:(site) isVisible:True))) (= (count Sites in:(intersection (sites {(site)}) (sites Support (id Mover)))) 0))
    						(remove (site))
        				)
        			)
        		)
    		)
        )
        (end {
            (if 
            	(and 
            		(no Moves Next)
            		(> (count Sites in:(sites Occupied by:Mover)) (count Sites in:(sites Occupied by:Next)))
            	) 
            	(result Mover Win)
            )
            (if 
            	(and 
            		(no Moves Next)
            		(< (count Sites in:(sites Occupied by:Mover)) (count Sites in:(sites Occupied by:Next)))
            	) 
            	(result Next Win)
            )
            (if 
            	(and 
            		(no Moves Next)
            		(= (count Sites in:(sites Occupied by:Mover)) (count Sites in:(sites Occupied by:Next)))
            	) 
            	(result All Draw)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
		
	(info
		{
		(description "Margo is a board game similar to Go, played with marbles which may stack upwards. A special capture rule means that pinned pieces survive capture as zombies.")
		(rules "Two players, White and Black, each have at least enough marbles of their colour to cover the board. Start: The board is initially empty. White places the first piece at any board point, then Black may elect to swap colours in lieu of making the next move (swap rule). Play: Players then take turns placing a piece of their colour either: 1) at an empty board point, or 2) stacked on top of four existing pieces (of any colour). Players may not pass. Over/Under Rule: A connection crossing over an enemy connection cuts it. Each group therefore consists of a visibly connected set of pieces when viewed from above. It is not permitted to place a piece without freedom, unless that move captures neighbours to create its own freedom. It is not permitted to repeat the board position of the previous turn. The game ends when the current player has no legal move. The player with the most pieces in play wins (draw if equal).")
		(source "<a href=\"http://cambolbro.com/games/margo/margo-basics-41.pdf" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\">SHIBUMI Rulebook</a>")
	    (version "1.0.0")
	    (classification "wip_cedric/Capture")
	    (author "Cameron Browne")
	    (publisher "Computational Creativity Group, nestorgames")
	    (credit "CÃ©dric Antoine")
	    (date "2006")
		}
	)
    
	(graphics {
		(piece Scale "Ball" 1.0)
	    (board Style Shibumi)
	    (piece Colour P1 fillColour:(colour White))
	    (piece Colour P2 fillColour:(colour Black))
	})
       
)